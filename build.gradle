import de.undercouch.gradle.tasks.download.Download

plugins {
  id 'base'
  id 'maven-publish'
  id 'com.jfrog.bintray' version '1.8.0'
  id 'de.undercouch.download' version '3.3.0'
}

group = 'io.github.ssoloff'

ext {
  artifactId = "trident${packageName ? '-' + packageName : ''}"
  artifactDescription = "Trident animation library${packageName ? ' (' + packageName + ')' : ''}"
  tridentArtifactJarFileName = "${artifactId}-${version.endsWith('.00') ? version - '.00' : version}.jar"
  tridentArtifactJar = file("$libsDir/$tridentArtifactJarFileName")
}

task downloadTridentArtifactJar(type: Download) {
    src "https://github.com/kirill-grouchnikov/trident/raw/master/drop/$version/$tridentArtifactJarFileName"
    dest tridentArtifactJar
    overwrite false
}

bintrayUpload.dependsOn(downloadTridentArtifactJar)

publishing {
  publications {
    maven(MavenPublication) {
      artifact tridentArtifactJar
      artifactId project.artifactId
    }
  }
}

bintray {
  user = bintrayUser
  key = bintrayKey
  dryRun = bintrayDryRun.toBoolean()
  publish = true
  publications = ['maven']

  pkg {
    repo = 'maven'
    name = "io.github.ssoloff:$artifactId"
    desc = artifactDescription
    websiteUrl = 'https://github.com/ssoloff/trident-artifacts'
    issueTrackerUrl = 'https://github.com/ssoloff/trident-artifacts/issues'
    vcsUrl = 'https://github.com/ssoloff/trident-artifacts.git'
    licenses = ['BSD 2-Clause']
    publicDownloadNumbers = true

    version {
      name = "${project.version}"
      desc = "$artifactDescription ${project.version}"
    }
  }
}
